; this thing below, actualy DOES
; display the list generated by the (disabled) option generate "colored" vector list
; but it so damn SLOW that it is not useful!
; therfore I disabled the option,
; for the sake of the thought I stuck into it, I'll keep it here

                    direct   $d0 
;
INTENSITY_A         macro    
                    STA      VIA_port_a                   ;Store intensity in D/A 
                    LDD      #$0504                       ;mux disabled channel 2 
                    STA      VIA_port_b                   ; 
                    STB      VIA_port_b                   ;mux enabled channel 2 
                    LDB      #$01                         ; 
                    STB      VIA_port_b                   ;turn off mux 
                    endm     
; expects point to list in X
; intensity, rely, relx, intensity, rely, relx ... -X
draw_vl_intensity: 
                    lda      #SCALEX
                    sta      VIA_t1_cnt_lo                ; using a fixed timer enables us to switch shift of exactly at the right moment 
nextVector: 
                    lda      ,x+ 
                    bpl      goOn 
; next line
                    lda      #SCALEY
                    sta      VIA_t1_cnt_lo                ; using a fixed timer enables us to switch shift of exactly at the right moment 
                    lda      #-10                         ; some "size" in y 
                    STA      VIA_port_a 
                    lda      #$81 
                    sta      VIA_port_b 
                    dec      VIA_port_b 
                    nop      
                    inc      VIA_port_b 
; y is set 
                    clr      VIA_port_a                   ; x = 0 
                    CLR      VIA_t1_cnt_hi                ; enable timer 1 
                    CLRb     
                    LDA      #$40                         ;B-reg = T1 interrupt bit 
waity: 
                    BITA     VIA_int_flags                ;Wait for T1 to time out 
                    BEQ      waity                        ; 
                    STb      VIA_port_a 
                    lda      #$81 
                    sta      VIA_port_b 
                    dec      VIA_port_b 
                    nop      
                    inc      VIA_port_b 
; y is set 0
                    lda      #SCALEX
                    sta      VIA_t1_cnt_lo                ; using a fixed timer enables us to switch shift of exactly at the right moment 
                    lda      ,x+ 
                    bpl      goOn 
                    rts      

goOn: 
                    INTENSITY_A  
; intensity is set
                    LDB      ,X+                          ; b = x 
                    stb      VIA_port_a 
; x is set 
                    CLR      VIA_shift_reg                ; $ff 
                    DEC      VIA_shift_reg                ; in shift register 
; shift is set
                    CLR      VIA_t1_cnt_hi                ; enable timer 1 
                    CLRb     
                    LDA      #$40                         ;B-reg = T1 interrupt bit 
waitx: 
                    BITA     VIA_int_flags                ;Wait for T1 to time out 
                    BEQ      waitx                         ; 
                    CLRA                                  ;Wait a moment more 
                    STA      VIA_shift_reg                ;Clear shift register (blank output) 
                    BRA      nextVector 
