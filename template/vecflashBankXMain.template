                    Bank 0
;***************************************************************************
; DEFINE SECTION
;***************************************************************************
; load vectrex bios routine definitions
                    INCLUDE  "VECTREX.I"                  ; vectrex function includes
;***************************************************************************
; Variable / RAM SECTION
;***************************************************************************
; insert your variables (RAM usage) in the BSS section
; user RAM starts at $c880 
                    BSS      
                    ORG      $c880                        ; start of our ram space 
bankToSwitchTo      ds       1 
switchRoutineRAMStart  =     bankToSwitchTo+1 
switchRoutineRAMEnd  =       switchRoutineRAMStart + (switchRoutineROMEnd - switchRoutineROMStart) ; $15
BANK_MAX            EQU      3+1 
;***************************************************************************
; HEADER SECTION
;***************************************************************************
; The cartridge ROM starts at address 0
                    CODE     
                    ORG      0 
; the first few bytes are mandatory, otherwise the BIOS will not load
; the ROM file, and will start MineStorm instead
                    DB       "g GCE 1998", $80 ; 'g' is copyright sign
                    DW       music1                       ; music from the rom 
                    DB       $F8, $50, $20, -$80          ; hight, width, rel y, rel x (from 0,0) 
                    DB       "BANK SW", $80               ; some game information, ending with $80
                    DB       0                            ; end of game header 
;***************************************************************************
; CODE SECTION
;***************************************************************************
; here the cartridge program starts off
                    clr      bankToSwitchTo 
                    inc bankToSwitchTo; first is reset
                    JMP      main 

main: 
                    JSR      Read_Btns                    ; get one status first, for 
                                                          ;        the difference 
                    JSR      Wait_Recal                   ; Vectrex BIOS recalibration 
                    JSR      Intensity_5F                 ; Sets the intensity of the 
                                                          ;        vector beam to $5f 
                    LDU      #hello_world_string          ; address of string 
                    LDA      #$10                         ; Text position relative Y 
                    LDB      #-$50                        ; Text position relative X 
                    JSR      Print_Str_d                  ; Vectrex BIOS print routine 
                    JSR      Read_Btns                    ; get button status 
                    CMPA     #$00                         ; is a button pressed? 
                    BEQ      no_button                    ; no, than go on 
                    bra      switchToNextBank             ; switch to next bank 

no_button: 
                    BRA      main                         ; and repeat forever 

;***************************************************************************
; DATA SECTION
;***************************************************************************
hello_world_string: 
                    DB       "BANK 0"                     ; only capital letters
                    DB       $80                          ; $80 is end of string 
;***************************************************************************
                                                          ;        This is Bank 0 
switchToNextBank: 
                    include  "vecflash_bs.i"
