; Note!
; The Scenario and Animation example routines use a signed 8 bit counter!
; This means, if an animation or scenario consists of more than 63 vectorlists
; the below offset to "X" (register A) must be expanded to 16 bit (register D)

;***************************************************************************
; DEFINE SECTION
;***************************************************************************
; load vectrex bios routine definitions
music1          EQU     $FD0D
VIA_t1_cnt_lo   EQU     $D004   ;VIA timer 1 count register lo (scale factor)

Intensity_5F    EQU     $F2A5   ;
Wait_Recal      EQU     $F192   ;
Mov_Draw_VLc_a  EQU     $F3AD   ;count y x y x ...
Moveto_d        EQU     $F312   ;
Reset0Ref       EQU     $F354   ;
VIA_cntl        EQU     $D00C   ;VIA control register

;***************************************************************************
; Variable / RAM SECTION
;***************************************************************************
; insert your variables (RAM usage) in the BSS section
; user RAM starts at $c880 
                    BSS      
                    ORG      $c880                ; start of our ram space 

sceneCounter        EQU      $c880

;***************************************************************************
; HEADER SECTION
;***************************************************************************
; The cartridge ROM starts at address 0
                    CODE     
                    ORG      0 
; the first few bytes are mandatory, otherwise the BIOS will not load
; the ROM file, and will start MineStorm instead
                    DB       "g GCE 1998", $80    ; 'g' is copyright sign
                    DW       music1               ; music from the rom 
                    DB       $F8, $50, $20, -$80  ; hight, width, rel y, rel x (from 0,0) 
                    DB       "SCENE MOV DRAW VLC", $80  ; some game information, ending with $80
                    DB       0                    ; end of game header 

;***************************************************************************
; CODE SECTION
;***************************************************************************
; here the cartridge program starts off
main: 
                    JSR      Wait_Recal           ; Vectrex BIOS recalibration 
                    JSR      Intensity_5F         ; Sets the intensity of the 
                                                  ; vector beam to $5f 
                    LDA      #$50                 ; scalefactor
                    STA      VIA_t1_cnt_lo
                    LDA      #$00                 ; position relative Y 
                    LDB      #$00                 ; position relative X
                    JSR      Moveto_d             ; sets up VIA control register after a wait recal

                    CLR      sceneCounter
nextScenePart:
                    LDA      sceneCounter
                    ASLA                          ; times two since it is a word pointer
                    LDX      #vData               ; address of data 
                    LDX      a,X
                    JSR      Mov_Draw_VLc_a       ; Vectrex BIOS print routine 
                    JSR      Reset0Ref
                    LDA      #$CE
                    STA      VIA_cntl

                    INC      sceneCounter
                    LDA      sceneCounter
                    CMPA     #vDataLength
                    BNE      nextScenePart
                    BRA      main                 ; and repeat forever 

;***************************************************************************
; DATA SECTION
;***************************************************************************
