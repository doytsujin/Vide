:title Joyport Devices

:h2 Part one - Buttons!
Below I mainly explain how to use buttons to "transport" data to a device
connected to the joyport.
If you understand the below things - it will be easy to read such simple things as buttons states. So - for now I will not explain that :-)!

Vectrex has two joyports, both are wired:
:div code
 ::
Vectrex Controller & Port Connections:~~
________Pin 1: Button 1~~
________Pin 2: Button 2~~
________Pin 3: Button 3~~
________Pin 4: Button 4~~
________Pin 5: Joystick: Horizontal~~
________Pin 6: Joystick: Vertical~~
________Pin 7: +5V~~
________Pin 8: Ground~~
________Pin 9: -5V~~
:/div
For easier writing, in the following I will say "button x" whenever I refer to "Pin x". 
And what I really mean is: the device connected to Pin x.
		
	~~
~~	
**Specials:~~**
Port 0 Button 4 is wired to 6809 FIRQ line~~
Port 1 Button 4 is wired to 6522 CA1 line~~

Both lines are DIRECTLY connected to the button outputs, these connections are not 
going thru PSG chip first!

Devices (except for e.g. paddles) on the joyports usually only use the four "buttons" to communicate with vectrex
(Lightpen, Imager, LinkCables, VecSpeech).

Although in theory all devices can be plugged into both ports usually only port 1 is used for these devices, since:
a) the vectrex "user" is used to having a joystick in port 0
b) all programs making use of the devices have to adjust to the ports, and they usually don't do that

The only device known, that actually IS used in port 0 is the lightpen.
There is one program (Engine Analyzer) which expects the lightpen in port 0.


**~~Communication~~**

There are  two types of communication possible:

~~**direct**~~
Uni directional communication: Device -> vectrex.

The direct communication can only be used FROM the device TO vectrex.
This can only be done via "button 4" which is connected to CA1 or FIRQ.


~~**indirect**~~
Bi directional communication is possible using the PSG chip.

Port 0 button 1-4 are connected to the four lower bits of Port A of the PSG chip.~~
(button 1 to bit 0, button 2 to bit 1, button 3 to bit 2, button 4 to bit 3)~~
Port 1 button 1-4 are connected to the four upper bits of Port A of the PSG chip.~~
(button 1 to bit 4, button 2 to bit 5, button 3 to bit 6, button 4 to bit 7)

~~//interlude PSG//~~ ~~
It is worth noting, that the Port A has two modes:
- Input mode (port A of PSG can be written to from the outside[a device in the joyports])
- Output mode (port A of PSG can output data to the outside [a device in the joyports])

The mode of the port is set by bit 6 of register 7 of the PSG (0 = input, 1 = output).

It seems, that internally the PSG has a special storage area for output values.
This "storage" is not effected by the values which are put to the port A lines (from the outside).
Upon switching from input to output mode, the values from that internal storage are 
always restored and set to port A.

In INPUT-mode~~
It seems that the output values can ALSO be set while PSG is in input mode.
(Setting output data while in input mode, sets the data to that internal "storage" area, 
the actual port A will always read $ff when read[from the outside] while in input mode.)

In OUTPUT-mode~~
The opposite does not seem to be true.
When PSG is in output mode, it seems data can still be "correctly" read from the PSG side 
(meaning: reading from the vectrex side, reading out PSG register (reg 14) reads the state 
of the buttons (NOT $ff) - my first guess would have been to read a $ff 
if in output mode - but this is NOT the case)~~
[I have not written extensive test programs for this behaviour. I don't know if the
returned value is the CURRENT input value on the ports, or the last known input value. In the
later case that would mean, that there also is an internal storage area for input values, 
as there is for output values. The known implementation of the game BerzerkArena seem to indicate, that even though port A of the PSG is set to output mode, input values (coming in this case from the other vectrex) can be read as "usual". The complete game BerzerkArena is played with PSG Port A set to output, and the serial communication works in both directions.]

~~//interlude end//~~

~~**Accessing the Buttons**~~
As mentioned above, the joyport buttons are directly connected to PSG port A.
To read or write to the buttons the programmer must access Port A of PSG.

The PSG on the other hand is not connected/mapped to any memory location. To access PSG 
one has to go through VIA 6522.

Now it gets confusing. ~~VIA also has a Port A (Reg 1), the lines of VIA port A are
"passed" through the PSG chip to the DAC. We are ignoring DAC and all that follows
at the moment and just acknowledge the fact, that PSG chip is connected 
(with its DA0-7 lines [DA stands for Data/Address, you will see why later]) to VIA.~~ ~~
So - at the end of the day to read or write values to the PSG one has to go thru VIA (port A).
(Needless to say, that Via port A also can be input or output, which in turn is configured by Via Reg 3 DDRA).

Now, PSG has only one 8 bit "line" (DA0 - DA7) as Input/Output for data. But we also have to tell
the PSG what we want to do (read or write, and from where or where to). For that purpose there are two
other PSG "pins" available (in reality 3, but we are ignoring BC2 here).~~
Pin: ~~
________BDIR (Bus direction)~~
________BC1 (Bus Control 1)~~

Those two pins taken together can be read as a two bit number with following meaning:~~
BDIR/BC1: 00 - inactive~~
BDIR/BC1: 01 - read from PSG~~
BDIR/BC1: 10 - write to PSG~~
BDIR/BC1: 11 - latch address~~

With configuring these pins in conjunction with the DA lines we access the PSG and can read or write values.

Simple example:~~
 Task: read value of register 7 ~~ ~~
We set following "things" of PSG:~~
________DA       =  7~~
________BDIR/BC1 = 00~~
________BDIR/BC1 = 11~~
________BDIR/BC1 = 00~~
________BDIR/BC1 = 01~~
________DA can now be read with the value of PSG Reg 7~~
  ~~
Comments:
1) first set the register we are interested in to DA ~~(hence D"A" (ADDRESS we want to read\))
2) set BDIR/BC1 = 00 to INACTIVE, this is NEEDED!~~
   Switching the state of BDIR/BC1 can not be done directly!~~
   Switching to a state (different 00) must ALLWAYS be done from state 00!
3) set BDIR/BC1 = 11, PSG remembers the value in DA (7) as the current latched address~~
   meaning: the next read or write access will be done with that latched address
4) set BDIR/BC1 = 00 see above
5) set BDIR/BC1 = 01 read the currently latched register (7) to DA ~~(hence "D"A 
  (since now it is filled with DATA of reg 7\)) ~~ 

 
~~**BDIR and BC1**~~
These two pins are again set (can not be read!) by setting appropriate bits to VIA.~~
More specific both pins are connected to VIA port B.~~
Even still more specific:~~
________BDIR is bit 4 of via port B~~
________BC1 is bit 3 of via port B~~
  ~~
 
Real world vectrex example:

Writing a value to joystick buttons 

(value of A is "split" to two nibbles, ~~
the lower nibble will end up in Port 0 buttons~~
the upper nibble will end up in port 1 buttons~~
\- with vectrex, writings to port 0 will usually be discarded~~ 
by an ignorant joystick :-) ) 

::~~
; value given in A~~
; write value to joystick ports (buttons)~~
; Port A of PSG is kept in OUTPUT mode~~
; assuming that port A of VIA is in output mode~~
; and also that port B of VIA is otherwise configured to correct IO settings~~
write_A_toJoy2:~~
________sta -01,s______________; store given value on stack~~
~~
; Latch Reg $07 of PSG~~
________lda #$07_______________; Register 7 of PSG~~
________sta <VIA_port_a________; VIA A = 0x07 ~~
________ldd #$9981_____________; 1001 1001 / 1000 0001 1) bit 3+4 == 11, 2) bit 3+4 = 00 ~~
________sta <VIA_port_b________; VIA Port B = 99, mux disabled, RAMP disabled, BC1/BDIR = 11 (Latch address)~~
________stb <VIA_port_b________; VIA Port B = 81, mux disabled, RAMP disabled, BC1/BDIR = 00 (PSG inactive)~~
     ~~
; write value of $7f to reg $07 (latched)~~
________lda #$7F_______________; $7f to VIA A (DAC) $7f = 0111 1111, disable all noise and tone in psg, ~~
_______________________________; ENABLE OUTPUT on port A of PSG (port B irrelevant since it is a 8912)~~
________sta <VIA_port_a________; $7f to VIA A (DAC)~~
________ldd #$9181~~
________sta <VIA_port_b________; VIA Port B = 91, mux disabled, RAMP disabled, BC1/BDIR = 10 (Write to PSG)~~
________stb <VIA_port_b________; VIA Port B = 81, mux disabled, RAMP disabled, BC1/BDIR = 00 (PSG inactive)~~
~~
; now port A of PSG is in output mode and can be written to	 ~~
	 ~~
; Latch Reg $E of PSG (Port A)~~
________lda #$0E~~
________sta <VIA_port_a________; $0e to VIA A (DAC)~~
________ldd #$9981_____________; 1001 1001 / 1000 0001 1) bit 3+4 == 11, 2) bit 3+4 = 00 ~~
________sta <VIA_port_b________; VIA Port B = 99, mux disabled, RAMP disabled, BC1/BDIR = 11 (Latch address)~~
________stb <VIA_port_b________; VIA Port B = 81, mux disabled, RAMP disabled, BC1/BDIR = 00 (PSG inactive)~~
     ~~
; write A to port~~
________lda -01,s______________; read value from stack~~
________sta <VIA_port_a________; output value to via VIA port A to Port A of PSG (Joystick buttons)~~
________ldd #$9181_____________; 1001 0001 / 1000 0001 1) bit 3+4 == 10, 2) bit 3+4 = 00 ~~
________sta <VIA_port_b________; VIA Port B = 91, mux disabled, RAMP disabled, BC1/BDIR = 10 (Write to PSG)~~
________stb <VIA_port_b________; VIA Port B = 81, mux disabled, RAMP disabled, BC1/BDIR = 00 (PSG inactive)~~
________rts~~
~~
::
Note:~~
BC1/BDIR = 00 is used at the end of each command. This is ok, if this is followed strictly,
since before each setting BC1/BDIR must be 00, which is still "honoured".

:h2 Part two - Joystick!
Reading values of the joystick is not as easy as one might think.~~
(Or - you just use the available BIOS functions - THAT makes it quite easy, but that is explained elsewhere).

The vectrex was one of the first machines to have analog joysticks. Below I will try to explain how they are implemented and accessed.

Each joystick direction is implemented as a potentiometer. Alltogether there can be four such potentiometers:
- port 0: horizontal
- port 0: vertical
- port 1: horizontal
- port 1: vertical

Do you remember how the integrators have access to DAC values?
The DAC value goes "thru" a multiplexer. The multiplexer has two "SELECT" lines (Via port B bit 1+2). The select values "multiplex" the one input signal to one of four output lines. The output line is chosen by the value of the select lines.

Why am I telling you that?

Because with the joystick potentiometers this is the same.

REALLY the same, since the physical electronic part inside the vectrex is the same (4052b). That chip is a Multiplexer/DeMultiplexer.
The same select lines (Via port B bits 1+2) select which of the 4 potentiometers is taken as input:~~
Bit2+Bit1:
- 00: port 0 horizontal
- 01: port 0 vertical
- 10: port 1 horizontal
- 11: port 1 vertical

The demultiplexer sets the voltage of the one selected potentiometer to a circuit which is connected to the DAC and a special other pin.

The circuit compares the value of the potentiomenter to the analog value received from the DAC. If the contents of the DAC (the analog representation of a digital signed value ranging from -128 to +127) is smaller than the value gotten from the potentiometer, than the above mentioned pin is set to true - otherwise false.

The pin (how could it be otherwise) is again part of Via - it can be found in Via port B bit 5. The bit/pin is cleverly named "comparator input bit" (meaning: input //from// the comparator, not input //to// the comparator!).

With that information, a digital request in pseudo code can be done like:

Question: is Joystick port B moved to right?~~
~~
- disable mux~~
- dac = 0~~
- mux sel = 00~~
- enable mux~~
- delay short~~
- disable mux~~
- delay "long"~~
- load dac with some positive value~~
- check compare bit~~
- if set than joystick was moved right~~

To get an analog value in principle the same must be done, but you have to test more. The line "load dac with some positive value" - and the checking afterwards has to be done with increasing values till a point is reached where the flag is not set anymore. The last set value (or the first unset value) is the digital representation of the potentiometer of the current joystick axis.

This is, why it is usually a good idea to only use analog values if it absolutely neccessary, since it is time consuming to do the aproximation.

Real life example of a digital joystick "request" for port 0:~~
(not the BIOS function)~~
~~
::
queryInputPort0: ~~
________lda      #$D0________________; setup DP ~~
________tfr      a,dp ~~
________direct   $D0 ~~
________lda      #$03________________; mux disabled, mux sel = 01 (vertical pot port 0) ~~
________sta      <VIA_port_b_________; ~~
________clr      <VIA_port_a_________; dac = 0 ~~
________dec      <VIA_port_b_________; mux enabled, mux sel = 01 ~~
________ldb      #$20________________; a wait loop 32 times a loop (waiting for the pots to "read" values, and feed to compare logic) ~~
waitLoopV: ~~
________decb____________________; ... ~~
________bne      waitLoopV___________; wait... ~~
________inc      <VIA_port_b_________; disable mux ~~
________ldb      #$20________________; load b with comparator bit (0010 0000) ~~
________lda      #$40________________; load a with test value (positive y) ~~
________sta      <VIA_port_a_________; test value to DAC ~~
________lda      #$01________________; default result value y was pushed UP ~~
________bitb     <VIA_port_b________; test comparator ~~
________bne      yReadDone___________; if comparator not cleared - joystick was moved up ~~
________neg      <VIA_port_a_________; "load" with negative value ~~
________nega____________________; also switch the possible result in A ~~
________bitb     <VIA_port_b________; test comparator again ~~
________beq      yReadDone___________; if cleared the joystick was moved down ~~
________clra____________________; if still not cleared, we clear a as the final vertical test result (no move at all) ~~
yReadDone: ~~
________sta      >joy_y______________; remember the result in "our" joystick data ~~
;~~
; now the same for horizontal~~
________lda      #$01________________; mux disabled, mux sel = 00 (horizontal pot port 0) ~~
________sta      <VIA_port_b_________; ~~
________clr      <VIA_port_a_________; dac = 0 ~~
________dec      <VIA_port_b_________; mux enabled, mux sel = 01 ~~
________ldb      #$20________________; a wait loop 32 times a loop (waiting for the pots to "read" values, and feed to compare logic) ~~
waitLoopH: ~~
________decb____________________; ... ~~
________bne      waitLoopH___________; wait... ~~
________inc      <VIA_port_b_________; disable mux ~~
________ldb      #$20________________; load b with comparator bit (0010 0000) ~~
________lda      #$40________________; load a with test value (positive x) ~~
________sta      <VIA_port_a_________; test value to DAC ~~
________lda      #$01________________; default result value x was pushed right ~~
________bitb     <VIA_port_b________; test comparator ~~
________bne      xReadDone___________; if comparator not~~ cleared - joystick was moved right ~~
________neg      <VIA_port_a_________; "load" with negative value ~~
________nega____________________; also switch the possible result in A ~~
________bitb     <VIA_port_b________; test comparator again ~~
________beq      xReadDone___________; if cleared the joystick was moved left ~~
________clra____________________; if still not cleared, we clear a as the final vertical test result (no move at all) ~~
xReadDone: ~~
________sta      >joy_x______________; remember the result in "our" joystick data ~~
________rts_____________________; done ~~

~~
